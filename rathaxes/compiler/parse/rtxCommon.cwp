/*
 *
 * This file requires the following rules to be defined :
 * - identifier
 *
 */


/*
 *
 * Here are the rules for the base builtin types parsing
 *  - scoped_identifier
 *  - serial value
 *  - numeric value
 *  - string value
 */
rtx_scoped_identifier(theNode : node, force_scope : value) ::=
    #!ignore
/**/=> local sScope = "";
/**/=> local globally_defined = false;
    [
        #readIdentifier:sScope "::"
    |
        => sScope = "";
        "::"
        => globally_defined = true;
    ]?
    #check(force_scope == false
           || (force_scope == true && globally_defined == true)
           || (force_scope == true && sScope != ""))
    #readIdentifier:sIdentifier
    #check(rtxNodeIdentifier(theNode, sIdentifier, sScope, globally_defined))
;

// This rule dissects an identifier in parts separated by dots.
rtx_composed_identifier(theNode : node) ::=
/**/=>local theVar;
    rtx_scoped_identifier(theVar, false)
    // Create the node from
    #check(rtxNodeVar(theNode, theVar))
    #check(rtxNode_InfoLocation(theNode))
    [
        '.' identifier(theNode):curField
        // add the field into the block
        #check(rtxNodeVar_AddIdentifier(theNode, curField))
    ]*
;

rtx_numeric(theNumber : node) ::=
    #readInteger:number
    #check(rtxNodeNumber(theNumber, number))
;

// A variable/parameter for a list of variable/parameters
rtx_var(theVar : node) ::=
    [
        rtx_composed_identifier(theVar)
    |
        rtx_numeric(theVar)
    ]
;


// Here we can identify a list of template variables
rtx_var_list(theBlock : node) ::=
/**/=> local lastNode;
    rtx_var(lastNode)
    #check(rtxNodeVarList_AddNode(theBlock, lastNode))
    [
        ','
/**/    => local lastVar;
        rtx_var(lastVar)
        #check(rtxNodeVarList_AddNode(theBlock, lastVar))
    ]*
;

rtx_serial_separator ::=
    #!ignore  ['.' | '_' | '-']
;
rtx_serial_unit ::=
 #!ignore [ #noCase 'A'..'Z' | '0'..'9' ]+
;

rtx_serial(theSerial : node) ::=
    #!ignore
    rtx_serial_unit:sElement
/**/=> local unit;
    #check(rtxNodeSerialUnit(unit, sElement))
    #check(rtxNodeSerial_AddUnit(theSerial, unit))
    [
        rtx_serial_separator:sSep
/**/    => local theSeparator;
        #check(rtxNodeSerialSep(theSeparator, sSep))
        rtx_serial_unit:sElem
/**/    => local theUnit;
        #check(rtxNodeSerialUnit(theUnit, sElem))
        #check(rtxNodeSerial_AddUnit(theSerial, theSeparator))
        #check(rtxNodeSerial_AddUnit(theSerial, theUnit))
    ]+
;

rtx_value(theValue : node) ::=
        /*
         * This bit of code should be valid for any number (integer/float/etc...)
         * and for the simple identifiers and c_strings.
         */
        [ #readCString | #readCChar | [~[','|')'|'{'|';']]+ ]:data
        #check(rtxType_Search(data, theValue))

//      #readIdentifier:sId
//      // Prevent next char from being a serial sep. (or its a serial value)
//      [ #!ignore !rtx_serial_separator ]
//      #check(rtxNodeIdentifier(theValue, sId))
//  |
//      #readInteger:sInt
//      // Prevent next char from being a serial sep. (or its a serial value)
//      // AND     next char from being an alpha char. (or its a serial value)
//      [ #!ignore ![ [#noCase 'A'..'Z'] | rtx_serial_separator ] ]
//      #check(rtxNodeNumber(theValue, sInt))
    |
        #check(rtxNodeSerial(theValue))
        rtx_serial(theValue)
//    |
///**/    => local sString;
//       string_constant(sString)
//       #check(rtxNodeString(theValue, sString))
;



/*
 *
 * Here are the rules for the parsing of the template sequence prototypes
 * It is used both in the backend and in the middle-end
 *
 */

rtx_param(theParams : node, theIdentifiers : node) ::=
    #pushItem(theIdentifiers)
    #pushItem(theParams)
    rtx_scoped_identifier(theParams#back, false)
    [    #readIdentifier:theIdentifiers#back ]?
;

rtx_parameter_list(theParams : node, theIdentifiers : node) ::=
    '('
        [
            rtx_param(theParams, theIdentifiers)
            [
                ','
                #continue(sError, "Expected parameter after ',' in parameter list.")
                rtx_param(theParams, theIdentifiers)
            ]*
        ]?
        #continue(sError, "Expected parenthesis closing the parameter list.")
    ')'
;

rtx_template_prototype(theRtype : node, theIdentifiers : node) ::=
/**/=> local nameNode;
/**/=> local params;
    rtx_scoped_identifier(nameNode, false)
    #check(rtxNodeRType(theRtype, nameNode, params))
    rtx_parameter_list(params, theIdentifiers)
;
