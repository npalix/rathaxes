#if !existFunction(rtxInterfaces_version)

function        rtxInterfaces_version()
{ return "1.0"; }

function        rtxItfCheckInheritance_walk<T>(interface_node : node,
                                       interfaces_list : node)
{
    traceLine("Unknown rtx_node :" + T);
	error("Unknown rtx_node :" + T);
}

function        rtxItfCheckInheritance_walk<"__rtx_itf__">(interface_node : node,
                                                           interfaces_list : node)
{
    foreach inherited_interface in interface_node.inheritance
    {
        local parentName = key(inherited_interface);

        if (interfaces_list.findElement(parentName) == false)
        {
            traceLine("Unable to find interface ``" + parentName
                      + "'' required by ``" + interface_node.name + "''");
            return false;
        }
        else
            ref interface_node.inheritance[parentName] = interfaces_list[parentName];
    }

    return true;
}

function        rtxInterfaceHasType(rtypeNode : node, interfaceNode : node)
{
    if (rtypeNode.type != "__rtx_rtype__")
        error("Bad node type");

    /* Check current interface. */
    local name = rtxRTypeName<rtypeNode.type>(rtypeNode);
    if (findElement(name + "{}", interfaceNode.types) == true)
        return true;


    traceLine(">>> Check type: " + rtypeNode.name.scope + "::" + rtypeNode.name.name + "in parent interfaces.");
    /* Recursive check in parents interfaces. */
    foreach parentInterface in interfaceNode.inheritance
    {
        if (rtxInterfaceHasType(rtypeNode, parentInterface) == true)
            return true;
    }

    return false;
}

function        rtxItfCheckTypesDeclarations<T>(currentDeclaration : node,
                                                currentInterface : node,
                                                interfacesList : node)
{
    traceLine("Specifier: " + T + " - skipping");
    return true;
}

function        rtxItfCheckTypesDeclarations<"variable">(currentDeclaration : node,
                                                         currentInterface : node,
                                                         interfacesList : node)
{
    local scope = currentDeclaration.rtype.name.scope;
    local name = currentDeclaration.rtype.name.name;
    local typeName = rtxRTypeName<currentDeclaration.rtype.type>(currentDeclaration.rtype);
    local ret = false;

    local varName = currentDeclaration.identifiers.scope + "::" + currentDeclaration.identifiers.name;
    traceLine("Searching for ``" + varName + "'' variable's type: " + typeName);

    if (scope == "")
    {
        traceLine("Type should be define in the global namespace");
        ret = true;
    }
    else
    {
        traceLine("Searching " + name + " in inherited interfaces");
        ret = rtxInterfaceHasType(currentDeclaration.rtype, currentInterface);
    }

    return ret;
}

function        rtxItfCheckTypesDeclarations_walk(currentInterface : node,
                                                  interfacesList : node)
{
    foreach item in currentInterface.body.block
    {
        if (item.type == "__rtx_itf_decl__")
        {
            if (rtxItfCheckTypesDeclarations<item.specifier>(item, currentInterface, interfacesList) == false)
                return false;
        }
    }
    return true;
}

#end    /* !existFunction(rtxInterfaces_version) */
