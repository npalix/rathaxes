// Define some color if available
#include "misc/colors.cws"

/*
 * Include Parsing dependencies.
 */
#include "cwTool.inc.cws"
#include "cnorm.inc.cws"
#include "cnorm2c/cnorm2c.inc.cws"
#include "patchLib/cnormPatchLib.inc.cws"
#include "rtxNode/rtxNode.inc.cws"

/*
 * Include the type-checking scripts
 */
#include "rtxType/rtxType.inc.cws"
#include "rtxInterfaces/rtxInterfaces.inc.cws"
#include "rtxFront/rtxTypeCheck.inc.cws"

/*
 * Include the cache-managing script
 */
#include "rtxLink/rtxLink.inc.cws"

/*
 * Include the backend annotation passes for the AST.
 */
#include "rtxTpl/rtxCompile.inc.cws"
#include "rtxTpl/rtxMeta.inc.cws"
#include "rtxTpl/rtxTypeHash.inc.cws"
#include "rtxTpl/rtxIntrospect.inc.cws"
#include "rtxTpl/rtxTypeCheck.inc.cws"
#include "rtxTpl/rtxGen.inc.cws"
#include "rtxTpl/rtxResolve.inc.cws"

#include "rtxFront/rtxGenerate.inc.cws"

function usage()
{
    traceLine(  "Usage: rathaxes generate (linux|openbsd|windows) file.rtx" + endl()
                + "    or rathaxes rti-check    (file.rti|file.rtx)" + endl()
                + "    or rathaxes rti-register (file.rti|file.rtx)" + endl()
                + "    or rathaxes blt-check    (file.blt|file.rtx)" + endl()
                + "    or rathaxes blt-register (file.blt|file.rtx)" + endl()
                + "    or cache install" + endl()
                + "    or cache clear");
}

function rtxError2String(errnode : node, with_stack : value = true)
{
        local out = "An error Occured while parsing :" + endl();
        if (with_stack != false)
        {
            out = out + "Trace:" + endl();
            foreach stack_line in errnode[0].trace
                out += stack_line;
            out += endl();
        }
        out += "Error: " + errnode[0].msg;
        return out;
}

function rtxParse(inputFile : value, source : node)
{
    // First setup cnorm...
    cnormDialect<"__std__">(source);
    parseAsBNF("rtxParse/rtx.cwp", source, inputFile);
    removeVariable(source.dialect);
    if (!isEmpty(source.error))
    {
        traceLine(rtxError2String(source.error));
        exit(-1);
    }
}

/*
 * First, setup the paths 
 */
insert this.rathaxes_path = _ARGS[0];
insert this.scripts_path  = this.rathaxes_path + "/" + "compiler"        + "/";
insert this.backlib_path  = this.rathaxes_path + "/" + "backend_library" + "/";
insert this.cache_path    = this.rathaxes_path + "/" + "cache"           + "/";
if (!existDirectory(this.rathaxes_path)){ createDirectory(this.rathaxes_path); }
if (!existDirectory(this.backlib_path)) { createDirectory(this.backlib_path); }
if (!existDirectory(this.cache_path))   { createDirectory(this.cache_path); }

insert this.local_rathaxes_path = _ARGS[1];
insert this.local_backlib_path  = this.local_rathaxes_path + "/" + "backend_library" + "/";
insert this.local_cache_path    = this.local_rathaxes_path + "/" + "cache"           + "/";
if (!existDirectory(this.local_rathaxes_path))  { createDirectory(this.local_rathaxes_path); }
if (!existDirectory(this.local_backlib_path))   { createDirectory(this.local_backlib_path); }
if (!existDirectory(this.local_cache_path))     { createDirectory(this.local_cache_path); }

insert this.operation = _ARGS[2];

if (toLowerString(this.operation) == "generate")
{
    local target_os = toLowerString(_ARGS[3]);
    local inputFile = _ARGS[4];

    if (rtxLink_LoadCache() == false)
        traceLine("Could not load cache. Trying to do without it...");

    insert this.front;

    cnormDialect<"__std__">(this.front);
    rtxType_Load(this.front);
    rtxParse(inputFile, this.front);

    if (rtxFront_TypeCheck(this.front) == false)
    {
        traceLine(RED + "[Error] Front-end implementation type"
                  + " checking failed. Stopping compilation." + DEFAULT_COLOR);
        exit (3);
    }

    if (rtxFrontGen_iterate<this.front.type>(this.front) == false)
    {
        traceLine(RED + "[Error] Driver could not be generated." + DEFAULT_COLOR);
        exit(4);
    }
    
    traceLine("Resulting code:"+cnorm2c(this.c_tree.c_block));
}
else if (   toLowerString(this.operation) == "blt-check"
         || toLowerString(this.operation) == "blt-register")
{
    local inputFile = _ARGS[3];
    local source;

    // Load cache first
    if (rtxLink_LoadCache() == false)
        traceLine("Could not load cache. Trying to do without it...");

    rtxParse(inputFile, source);

    // Compile the placeHolders
    foreach item in source.block
        rtxTpl_walk<item.type>(item);
    local has_error = false;
    foreach item in source.block
    {
        rtxMeta_walk<item.type>(item);
        if (!isEmpty(item.error))
        {
            traceLine("Item of type '"+item.type+"' could not be parsed :"
                      + endl() + item.error);
            has_error = true;
        }
    }
    if (has_error)
    {
        exit(-1);
    }

    // Now, use the two passes to annotate the types and 
    traceLine("Hashing types in instrumented C code...");
    rtxTypeHash(source);
    traceLine("Introspecting Instrumented C code...");
    rtxIntrospect(source);

    // After annotating the knowledge tree, We can now
    // check each template against the associated interfaces
    if (rtxTpl_TypeCheck(source) == false)
    {
        traceLine("Template Type Checking failed. Stopping template compilation.");
        exit(1);
    }
    traceLine("Template type checking suceeded");

    // Now generate the trees and codeworker files
    foreach item in source.block
    {
        local uid = 0;
        rtxGen<item.type>(item, uid);
        uid = $uid + 1$;
    }

    // And register the whole thing into the cache session.
    if (rtxLink_RegisterToCache(source) == false)
    {
        error("Could not register file into the cache !");
    }

    if (toLowerString(this.operation) == "blt-register")
    {
        if (rtxLink_SaveCache() == false)
            error("Could not save cache !");
    }
}
else if (toLowerString(this.operation) == "rti-check"
         || toLowerString(this.operation) == "rti-register")
{
    local inputFile = _ARGS[3];
    local source;

    // Load cache first
    if (rtxLink_LoadCache() == false)
        traceLine("Could not load cache. Trying to do without it...");

    rtxParse(inputFile, source);

    // XXX TODO FIXME
    // This snippet of code does not manage interfaces extensions.
    // Check that the interface's dependencies are valid.
    if (rtxItfCheckInheritance_walk<source.type>(source, source.interfaces) == false)
        exit(1);

    // Check that every type/pointcut used is defined in a parent interface.
    if (rtxItfCheckTypesDeclarations_walk(source, source.interfaces) == false)
        exit(2);
    
    // And register the whole thing into the cache session.
    if (rtxLink_RegisterToCache(source) == false)
    {
        error("Could not register file into the cache !");
    }

    if (toLowerString(this.operation) == "rti-register")
    {
        if (rtxLink_SaveCache() == false)
            error("Could not save cache !");
    }
}
else if (toLowerString(this.operation) == "cache")
{
    insert this.subop = _ARGS[3];
    if (toLowerString(this.subop) == "clear")
    {
        rtxLink_LoadCache();
        clearVariable(this.caches.local);
        rtxLink_SaveCache();
    }
    else if (toLowerString(this.subop) == "install")
    {
        rtxLink_InstallLocalCache();
    }
    else
    {
        usage();
        exit(1);
    }

}
else
{
    usage();
    exit(1);
}

exit (0);
