#include "cnorm.inc.cws"
#include "rtxNode/rtxNode.inc.cws"
#include "rtxLink.inc.cws"

function addGlobal(cache1 : node, cache2 : node, item : node)
{
    pushItem cache1.global_code;
    pushItem cache2.global_code;
    setall cache1.global_code#back = item;
    setall cache2.global_code#back = item;
}

function createGlobalCacheItem(tmp : node, nb : value)
{
    clearVariable(tmp);
    insert tmp.with;
    insert tmp.script_file = "global_code_script_"+nb+".cws";
    insert tmp.tree_file = "global_code_tree_"+nb+".tree";
}

function addChunk(cache1 : node, cache2 : node, template : node, chunk : node)
{
    local chunk_id;

    hashTemplatePrototype(template.rtype, chunk.tpl_id);

    if (!findElement(chunk.name, cache1.chunks))
        insert cache1.chunks[chunk.name];
    chunk_id = getArraySize(cache1.chunks[chunk.name]);
    pushItem cache1.chunks[chunk.name];
    setall cache1.chunks[chunk.name]#back = chunk;
    // Now insert the template containing the inserted chunk...
    if (!findElement(chunk.tpl_id, cache1.templates))
        insert cache1.templates[chunk.tpl_id];
    pushItem cache1.templates[chunk.tpl_id];
    setall cache1.templates[chunk.tpl_id]#back = template;
    insert cache1.templates[chunk.tpl_id]#back.chunks[chunk.name] = chunk_id;

    if (!findElement(chunk.name, cache2.chunks))
        insert cache2.chunks[chunk.name];
    chunk_id = getArraySize(cache2.chunks[chunk.name]);
    pushItem cache2.chunks[chunk.name];
    setall cache2.chunks[chunk.name]#back = chunk;
    // Now insert the template containing the inserted chunk...
    if (!findElement(chunk.tpl_id, cache2.templates))
        insert cache2.templates[chunk.tpl_id];
    pushItem cache2.templates[chunk.tpl_id];
    setall cache2.templates[chunk.tpl_id]#back = template;
    insert cache2.templates[chunk.tpl_id]#back.chunks[chunk.name] = chunk_id;
}

function createTemplateCacheItem(tmp : node, nb : value)
{
    local type_id;
    local params;
    local chunk_idx;

    clearVariable(tmp);
    insert tmp.with;
    insert tmp.rtype;
    rtxNodeIdentifier(type_id, "tpl_"+nb+"LKM");
    rtxNodeRType(tmp.rtype, type_id, params);
    pushItem params;
    rtxNodeIdentifier(params#back, "Context");
    if ($nb % 2$ == 1)
    {
        insert tmp.script_file = "template_mapping_script_"+nb+".cws";
        insert tmp.tree_file = "template_mapping_tree_"+nb+".tree";
    }
    insert tmp.chunks;
}

function createChunkCacheItem(tmp : node, nb : value)
{
    clearVariable(tmp);
    insert tmp.name = "chunk_"+$nb % 2$;
    insert tmp.with;
    insert tmp.tpl_id;
    insert tmp.script_file = "chunk_script_"+nb+".cws";
    insert tmp.tree_file = "chunk_tree_"+nb+".tree";
}

/*
 * First, build two fake cache trees to check against the expected result.
 */
local cache1;
local cache2;
local result;
local expected;

local tmp;
local tmp2;

insert cache1.global_code;
insert cache1.templates;
insert cache1.chunks;
insert cache2.global_code;
insert cache2.templates;
insert cache2.chunks;
insert expected.global_code;
insert expected.templates;
insert expected.chunks;


/*
 * First create two global_code items into each cache.
 */
createGlobalCacheItem(tmp, "1");
addGlobal(cache1, expected, tmp);
createGlobalCacheItem(tmp, "3");
addGlobal(cache1, expected, tmp);

createGlobalCacheItem(tmp, "2");
addGlobal(cache2, expected, tmp);
createGlobalCacheItem(tmp, "4");
addGlobal(cache2, expected, tmp);

/*
 * Then create two templates items into each cache, with their associated chunk.
 */
createTemplateCacheItem(tmp, "1");
createChunkCacheItem(tmp2, "1");
addChunk(cache2, expected, tmp, tmp2);

createTemplateCacheItem(tmp, "3");
createChunkCacheItem(tmp2, "3");
addChunk(cache2, expected, tmp, tmp2);

createTemplateCacheItem(tmp, "2");
createChunkCacheItem(tmp2, "2");
addChunk(cache2, expected, tmp, tmp2);

createTemplateCacheItem(tmp, "4");
createChunkCacheItem(tmp2, "4");
addChunk(cache2, expected, tmp, tmp2);

// And finally, merge the caches
rtxLink_MergeCaches(cache1, cache2, result);


if (!equalTrees(expected, result))
{
    traceLine("Expected result and actuel result differ !");
    //traceLine("Expected:\n"+toString(expected, true));
    //traceLine("Obtained:\n"+toString(result, true));
    exit(1);
}

exit (0);
