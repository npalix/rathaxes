IF (${CMAKE_SYSTEM_NAME} MATCHES "Linux")
    SET(CODEWORKER_BINARY_NAME codeworker)
ELSEIF (WIN32)
    SET(CODEWORKER_BINARY_NAME codeworker.exe)
ELSE (WIN32)
    MESSAGE(STATUS "No pre-built CodeWorker binary available for ${CMAKE_SYSTEM_NAME}")
ENDIF (${CMAKE_SYSTEM_NAME} MATCHES "Linux")

IF (CODEWORKER_BINARY_NAME AND NOT CODEWORKER_BINARY_PATH)
    SET(CODEWORKER_BINARY_PATH "${RATHAXES_BINARY_DIR}/maintainers/cnorm/${CODEWORKER_BINARY_NAME}")

    MESSAGE(STATUS "Downloading http://rathaxes.googlecode.com/files/${CODEWORKER_BINARY_NAME}")

    FILE(DOWNLOAD
         "http://rathaxes.googlecode.com/files/${CODEWORKER_BINARY_NAME}"
         ${CODEWORKER_BINARY_PATH}
         STATUS CODEWORKER_DL_STATUS
         TIMEOUT 5)

    LIST(GET CODEWORKER_DL_STATUS 0 CODEWORKER_DL_RETURN)
    LIST(GET CODEWORKER_DL_STATUS 1 CODEWORKER_DL_MESSAGE)

    IF (${CODEWORKER_DL_RETURN} EQUAL 0)
        SET(CODEWORKER_BINARY_PATH ${CODEWORKER_BINARY_PATH} CACHE PATH "Path to the downloaded pre-built CodeWorker binary")
        IF (UNIX)
            EXECUTE_PROCESS(COMMAND "chmod" "+x" "${CODEWORKER_BINARY_PATH}")
        ENDIF (UNIX)
    ELSE (${CODEWORKER_DL_RETURN} EQUAL 0)
        SET(CODEWORKER_BINARY_PATH "")
        MESSAGE(STATUS "Can't download the pre-built binary CodeWorker: ${CODEWORKER_DL_MESSAGE}")
    ENDIF (${CODEWORKER_DL_RETURN} EQUAL 0)
ENDIF (CODEWORKER_BINARY_NAME AND NOT CODEWORKER_BINARY_PATH)
